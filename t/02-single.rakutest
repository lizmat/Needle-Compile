BEGIN %*ENV<RAKU_TEST_DIE_ON_FAIL> = 1;

use Test;
use Needle::Compile;

my @nameds = (), :ignorecase, :ignoremark, :smartcase, :smartmark;

plan 2 + 5 * @nameds;

code ".subst('foo', 'bar')", "foo", "bar", 'simple .subst';
code ".uc",                  "foo", "FOO", 'simple .uc';

for @nameds {
    contains "foo", "foo", "bar", "find simple 'foo'", |$_;
}

for @nameds {
    starts-with "foo", "foo", "bar", "starts with simple 'foo'", |$_;
}

for @nameds {
    ends-with "foo", "foo", "bar", "ends with simple 'foo'", |$_;
}

for @nameds {
    equal "foo", "foo", "bar", "equal simple 'foo'", |$_;
}

for @nameds {
    words "foo", "foo", "bar", "words simple 'foo'", |$_;
}

#-------------------------------------------------------------------------------
# Infrastructure

# Adding a .type method to an object
my role Type {
    has $.type;
    method raku() { callsame() ~ " but Type('$!type')" }
}

# All possible tests for contains
my sub contains($target, $hit, $miss, $comment, *%_) is test-assertion {
    my $named := %_ ?? ", :%_.keys.head()" !! "";

    subtest "contains: $comment$named$named" => {
        my $ascode := ".contains('$target')";

        my @targets =
          $target,
          $target but Type("auto"),
          $target but Type("contains"),
          $target but Type("regex"),
          $ascode but Type("code"),
          "/$target/",
          "\{$ascode}",
          "*$ascode",
          auto     => $target,
          contains => $target,
          regex    => $target,
          code     => $ascode,
        ;

        my @autonots = @targets.map({ "!$_" if .WHAT =:= Str });

        plan 8 * @targets + 4 * @autonots;

        for @targets {
            my $needle := compile-needle($_, |%_);
            isa-ok $needle, Callable, "Testing $_.raku()";

            is-deeply $needle($hit),          True,  "hit '$hit'";
            is-deeply $needle("in{$hit}out"), True,  "hit 'in{$hit}out'";
            is-deeply $needle($miss),         False, "miss '$miss'";

            my $not := compile-needle( (not => $_,) );
            isa-ok $not, Callable, "Testing not $_.raku()";

            is-deeply $not($hit),          False, "not hit '$hit'";
            is-deeply $not("in{$hit}out"), False, "not hit 'in{$hit}out'";
            is-deeply $not($miss),         True,  "not miss '$miss'";
        }

        for @autonots {
            my $not := compile-needle($_, |%_);
            isa-ok $not, Callable, "Testing '$_'";

            is-deeply $not($hit),          False, "! hit '$hit'";
            is-deeply $not("in{$hit}out"), False, "! hit 'in{$hit}out'";
            is-deeply $not($miss),         True,  "! miss '$miss'";
        }
    }
}

# All possible tests for starts-with
my sub starts-with($target, $hit, $miss, $comment, *%_) is test-assertion {
    my $named := %_ ?? ", :%_.keys.head()" !! "";

    subtest "starts-with: $comment$named" => {
        my $auto   := "^$target";
        my $ascode := ".starts-with('$target')";

        my @targets =
          $target but Type("starts-with"),
          $auto,
          $auto   but Type("auto"),
          $auto   but Type("regex"),
          $ascode but Type("code"),
          "/^ $target/",
          "\{$ascode}",
          "*$ascode",
          starts-with => $target,
          auto        => $auto,
          regex       => $auto,
          code        => $ascode,
        ;

        my @autonots = @targets.map({ "!$_" if .WHAT =:= Str });

        plan 8 * @targets + 4 * @autonots;

        for @targets {
            my $needle := compile-needle($_, |%_);
            isa-ok $needle, Callable, "Testing $_.raku()";

            is-deeply $needle($hit),    True,  "hit '$hit'";
            is-deeply $needle(" $hit"), False, "miss ' $hit'";
            is-deeply $needle($miss),   False, "miss '$miss'";

            my $not := compile-needle( (not => $_,) );
            isa-ok $not, Callable, "Testing not $_.raku()";

            is-deeply $not($hit),    False, "not hit '$hit'";
            is-deeply $not(" $hit"), True,  "not miss ' $hit'";
            is-deeply $not($miss),   True,  "not miss '$miss'";
        }

        for @autonots {
            my $not := compile-needle($_, |%_);
            isa-ok $not, Callable, "Testing !$_";

            is-deeply $not($hit),    False, "! hit '$hit'";
            is-deeply $not(" $hit"), True,  "! miss ' $hit'";
            is-deeply $not($miss),   True,  "! miss '$miss'";
        }
    }
}

# All possible tests for ends-with
my sub ends-with($target, $hit, $miss, $comment, *%_) is test-assertion {
    my $named := %_ ?? ", :%_.keys.head()" !! "";

    subtest "ends-with: $comment$named" => {
        my $auto   := $target ~ '$';
        my $ascode := ".ends-with('$target')";

        my @targets =
          $target but Type("ends-with"),
          $auto,
          $auto   but Type("auto"),
          $auto   but Type("regex"),
          $ascode but Type("code"),
          "/$target \$/",
          "\{$ascode}",
          "*$ascode",
          ends-with => $target,
          auto      => $auto,
          code      => $ascode,
          regex     => $auto,
        ;

        my @autonots = @targets.map({ "!$_" if .WHAT =:= Str });

        plan 8 * @targets + 4 * @autonots;

        for @targets {
            my $needle := compile-needle($_, |%_);
            isa-ok $needle, Callable, "Testing $_.raku()";

            is-deeply $needle($hit),    True,  "hit '$hit'";
            is-deeply $needle("$hit "), False, "miss '$hit '";
            is-deeply $needle($miss),   False, "miss '$miss'";

            my $not := compile-needle( (not => $_,) );
            isa-ok $not, Callable, "Testing not $_.raku()";

            is-deeply $not($hit),    False, "not hit '$hit'";
            is-deeply $not("$hit "), True,  "not miss '$hit '";
            is-deeply $not($miss),   True,  "not miss '$miss'";
        }

        for @autonots {
            my $not := compile-needle($_, |%_);
            isa-ok $not, Callable, "Testing !$_";

            is-deeply $not($hit),    False, "! hit '$hit'";
            is-deeply $not("$hit "), True,  "! miss '$hit '";
            is-deeply $not($miss),   True,  "! miss '$miss'";
        }
    }
}

# All possible tests for equal
my sub equal($target, $hit, $miss, $comment, *%_) is test-assertion {
    my $named := %_ ?? ", :%_.keys.head()" !! "";

    subtest "equal: $comment$named" => {
        my $auto   := "^$target\$";
        my $ascode := "'$target' eq \$_";

        my @targets =
          $target but Type("equal"),
          $auto,
          $auto   but Type("auto"),
          $ascode but Type("code"),
          "/^ $target \$/",
          "\{$ascode}",
          auto  => $auto,
          regex => $auto,
          code  => $ascode,
        ;

        my @autonots = @targets.map({ "!$_" if .WHAT =:= Str });

        plan 8 * @targets + 4 * @autonots;

        for @targets {
            my $needle := compile-needle($_, |%_);
            isa-ok $needle, Callable, "Testing $_.raku()";

            is-deeply $needle($hit),     True,  "hit '$hit'";
            is-deeply $needle(" $hit "), False, "miss ' $hit '";
            is-deeply $needle($miss),    False, "miss '$miss'";

            my $not := compile-needle( (not => $_,) );
            isa-ok $not, Callable, "Testing not $_.raku()";

            is-deeply $not($hit),     False, "not hit '$hit'";
            is-deeply $not(" $hit "), True,  "not miss ' $hit '";
            is-deeply $not($miss),    True,  "not miss '$miss'";
        }

        for @autonots {
            my $not := compile-needle($_, |%_);
            isa-ok $not, Callable, "Testing !$_";

            is-deeply $not($hit),     False, "! hit '$hit'";
            is-deeply $not(" $hit "), True,  "! miss ' $hit '";
            is-deeply $not($miss),    True,  "! miss '$miss'";
        }
    }
}

# All possible tests for words
my sub words($target, $hit, $miss, $comment, *%_) is test-assertion {
    my $named := %_ ?? ", :%_.keys.head()" !! "";

    subtest "words: $comment$named" => {
        my $auto := "ยง$target";

        my @targets =
          $target but Type("words"),
          $auto,
          $auto   but Type("auto"),
          words => $target,
          auto  => $auto,
        ;

        my @autonots = @targets.map({ "!$_" if .WHAT =:= Str });

        plan 12 * @targets + 6 * @autonots;

        for @targets {
            my $needle := compile-needle($_, |%_);
            isa-ok $needle, Callable, "Testing $_.raku()";

            is-deeply $needle($hit),         True,  "hit '$hit'";
            is-deeply $needle(" $hit "),     True,  "hit ' $hit '";
            is-deeply $needle(":$hit:"),     True,  "hit ':$hit:'";
            is-deeply $needle("oo{$hit}ff"), False, "miss 'oo{$hit}ff'";
            is-deeply $needle($miss),        False, "miss '$miss'";

            my $not := compile-needle( (not => $_,), |%_ );
            isa-ok $not, Callable, "Testing not $_.raku()";

            is-deeply $not($hit),         False, "not hit '$hit'";
            is-deeply $not(" $hit "),     False, "not hit ' $hit '";
            is-deeply $not(":$hit:"),     False, "not hit ':$hit:'";
            is-deeply $not("oo{$hit}ff"), True,  "not miss 'oo{$hit}ff'";
            is-deeply $not($miss),        True,  "not miss '$miss'";
        }

        for @autonots {
            my $not := compile-needle($_, |%_);
            isa-ok $not, Callable, "Testing !$_";

            is-deeply $not($hit),         False, "! hit '$hit'";
            is-deeply $not(" $hit "),     False, "! hit ' $hit '";
            is-deeply $not(":$hit:"),     False, "! hit ':$hit:'";
            is-deeply $not("oo{$hit}ff"), True,  "! miss 'oo{$hit}ff'";
            is-deeply $not($miss),        True,  "! miss '$miss'";
        }
    }
}

# All other tests for code
my sub code($target, $source, $result, $comment) is test-assertion {
    subtest "code: $comment" => {
        my @targets =
          $target but Type("code"),
          "\{$target}",
          code => $target,
        ;
        @targets.push("*$target") if $target.starts-with(".");

        plan 2 * @targets;

        for @targets {
            my $needle := compile-needle($_);
            isa-ok $needle, Callable, "Testing $_.raku()";

            is-deeply $needle($source), $result, "'$source' transformed ok";
        }
    }
}

# vim: expandtab shiftwidth=4
